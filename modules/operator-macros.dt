#|
@module operator-macros

Provides utilities to generate operators that accept an arbitrary
number of arguments.  Such operators are already defined for the
following operators:

 - arithmetic operators: `+ - * / +' -' *' /'`; and
 - bitwise and logical or: `& | and or`.

|#
(module operator-macros (attr cto))

(import macros)
(import macros-core)
(import stdlib)

(namespace std (namespace macros
   
#|
@macro std.macros.def-identity-macro

Defines a macro that takes a single argument and returns it.

@param name Name of the macro.
@param linkage Linkage.
|#
(def def-identity-macro (macro extern (name linkage)
  (qq def (uq name) (macro (uq linkage) (value) value))))

#|
@macro std.macros.def-right-associative-macro

Defines a macro:
`(name a b rest) will be expanded to `(opname a (name b rest))`

@param name Name of the macro.
@param linkage Linkage.
@param opname Optional, defaults to `name`
|#
(def def-right-associative-macro (macro extern (name linkage opname)
  (qq do
    (def (uq name) (macro (uq linkage) (a rest)
      (using-namespace std.macros
        (let ((varargs-list \ (get-varargs-list mc (- (arg-count mc) 1) rest)))
          (qq (uq opname) (uq (uq a)) ((uq name) (uq (uql varargs-list)))))))))))

(def def-right-associative-macro (macro extern (name linkage)
  (qq std.macros.def-right-associative-macro (uq name) (uq linkage) (uq name))))

#|
@macro std.macros.def-left-associative-macro

Defines a macro:
`(name a b rest)` will be expanded to `(name (opname a b) rest)`

@param name Name of the macro.
@param linkage Linkage.
@param opname Optional, defaults to `name`
|#
(def def-left-associative-macro (macro extern (name linkage opname)
  (qq do
    (def (uq name) (macro (uq linkage) (a b rest)
      (using-namespace std.macros
        (let ((varargs-list \ (get-varargs-list mc (- (arg-count mc) 2) rest)))
          (qq (uq opname) ((uq name) (uq (uq a)) (uq (uq b)))
                          (uq (uql varargs-list))))))))))

(def def-left-associative-macro (macro extern (name linkage)
  (qq std.macros.def-left-associative-macro (uq name) (uq linkage) (uq name))))))

(mfor op (+  -  *  /
          +' -' *' /'
          &  | and or)
  (std.macros.def-left-associative-macro op extern))
